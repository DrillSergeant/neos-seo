prototype(Neos.Seo:RobotsTxt) < prototype(Neos.Fusion:Component) {
    // Every entry gets a line in the robots.txt
    data = Neos.Fusion:RawArray {
        userAgent = 'User-agent: *'
        disallow = 'Disallow: /neos/'
    }
    renderXMLSitemapLinks = true
    languageDimension = 'language'
    dimensionsPresets = ${Configuration.setting('Neos.Seo.robotsTxt.dimensionsPresets')}

    renderer = Neos.Fusion:Component {

        sitemaps = Neos.Fusion:Case {
            @if.render = ${props.renderXMLSitemapLinks}
            hasLanguage {
                condition = ${Configuration.setting('Neos.ContentRepository.contentDimensions.' + props.languageDimension) != null}
                renderer = Neos.Neos:DimensionsMenu {
                    dimension = ${props.languageDimension}
                    templatePath = 'resource://Neos.Seo/Private/Fusion/RobotsTxt/Sitemap.html'
                    presets = ${props.dimensionsPresets}
                    renderHiddenInIndex = true
                    includeAllPresets = true
                }
            }
            noLanguage {
                condition = true
                renderer = Neos.Neos:NodeUri {
                    absolute = true
                    format = 'xml.sitemap'
                    node = ${site}
                    @process.prefix = ${'Sitemap: ' + value}
                }
            }
        }

        data = ${props.data}

        linebreak = '
'
        renderer = Neos.Fusion:Http.Message {
            httpResponseHead.headers.Content-Type = 'text/plain;'
            body = afx`
                <Neos.Fusion:Collection collection={props.data} @children='itemRenderer'>
                    {item}{props.linebreak}
                </Neos.Fusion:Collection>
                {props.sitemaps}
            `
        }
    }
}

root.robotsTxt {
    condition = ${request.format == 'txt.robots'}
    renderer = Neos.Seo:RobotsTxt
}
