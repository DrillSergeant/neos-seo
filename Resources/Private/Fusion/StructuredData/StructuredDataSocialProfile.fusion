prototype(Neos.Seo:StructuredData.SocialProfile) < prototype(Neos.Fusion:Component) {
    @if.onHomepage = ${documentNode == site}
    @if.hasProfiles = ${Array.length(this.profiles) > 0}

    // 'type' can either be 'Person' or 'Organization' and should be set accordingly
    type = ${Configuration.setting('Neos.Seo.socialProfile.type')}
    twitterName = ${q(documentNode).property('twitterCardCreator') || Configuration.setting('Neos.Seo.socialProfile.profiles.twitter')}
    facebookName = ${Configuration.setting('Neos.Seo.socialProfile.profiles.facebook')}
    instagramName = ${Configuration.setting('Neos.Seo.socialProfile.profiles.instagram')}
    youTubeName = ${Configuration.setting('Neos.Seo.socialProfile.profiles.youTube')}
    linkedInName = ${Configuration.setting('Neos.Seo.socialProfile.profiles.linkedIn')}

    @context {
        type = ${this.type}
        twitterName = ${this.twitterName}
        facebookName = ${this.facebookName}
        instagramName = ${this.instagramName}
        youTubeName = ${this.youTubeName}
        linkedInName = ${this.linkedInName}
    }

    // Automatically add site url for organizations, see https://developers.google.com/search/docs/data-types/logo
    siteUrl = Neos.Neos:NodeUri {
        node = ${site}
        absolute = true
        @if.isOrganization = ${type == 'Organization'}
    }

    // Allow setting a logo path for organizations, see https://developers.google.com/search/docs/data-types/logo
    // Example: logo.path = 'resource://Vendor.Site/Public/Images/MyLogo.png'
    logo = Neos.Fusion:ResourceUri {
        path = ${Configuration.setting('Neos.Seo.socialProfile.logo')}
        @if.isOrganization = ${type == 'Organization' && !String.isBlank(this.path)}
    }

    // Automatically adds social profiles for a site owner, see https://developers.google.com/search/docs/data-types/social-profile
    profiles = Neos.Fusion:RawArray {
        twitter = ${'https://twitter.com/' + twitterName}
        twitter.@if.hasValue = ${!String.isBlank(twitterName)}

        facebook = ${'https://www.facebook.com/' + facebookName}
        facebook.@if.hasValue = ${!String.isBlank(facebookName)}

        instagram = ${'https://www.instagram.com/' + instagramName}
        instagram.@if.hasValue = ${!String.isBlank(instagramName)}

        youTube = ${'https://www.youtube.com/channel/' + youTubeName}
        youTube.@if.hasValue = ${!String.isBlank(youTubeName)}

        linkedIn = ${'https://www.linkedin.com/' + (type == 'Organization' ? 'company' : 'in') + '/' + linkedInName}
        linkedIn.@if.hasValue = ${!String.isBlank(linkedInName)}
    }

    renderer = afx`
        <script type="application/ld+json">
            <Neos.Seo:StructuredData.RootObject type={props.type}
                                                renderer.url={props.data.siteUrl} renderer.url.@if.isSet={props.data.siteUrl}
                                                renderer.logo={props.data.logo} renderer.logo.@if.isSet={props.data.logo}
                                                @children="renderer.sameAs">
                <Neos.Fusion:RawCollection collection={props.data.profiles} itemName="profile"
                                           iterationName="iteration" itemRenderer={profile}/>
            </Neos.Seo:StructuredData.RootObject>
        </script>
    `
}
